prototype(NeosRulez.Blog:BlogPosts) < prototype(Neos.Neos:Content) {
    templatePath = 'resource://NeosRulez.Blog/Private/Fusion/Content/BlogPosts/BlogPosts.html'

    node = ${q(node)}

    post = ${q(node).property('post')}
    blogpost = ${q(node).property('post')}

    pagebrowser = ${q(node).property('pagebrowser')}
    showdate = ${q(node).property('showdate')}
    showsubtitle = ${q(node).property('showsubtitle')}
    showaslist = ${q(node).property('showaslist')}
    blogcategories = ${q(node).property('blogcategories')}

    showAuthor = ${Configuration.setting('NeosRulez.Blog.showAuthor')}
    showDate = ${Configuration.setting('NeosRulez.Blog.showDate')}
    showCategory = ${Configuration.setting('NeosRulez.Blog.showCategory')}

    startingPoint = ${q(node).property('nodeidentifier')}
    sorting = ${q(node).property('sorting')}
    filter = Neos.Fusion:Collection {
        collection = ${q(node).property('blogcategories')}
        itemName = 'category'
        itemKey = 'key'
        itemRenderer = ${category.identifier + (iterator.isLast ? '' : ',')}
    }
    filterstring = ${'[blogcategories *= "' + this.filter + '"]'}
    blogposts = ${this.startingPoint ? q(this.startingPoint).children('[instanceof NeosRulez.Blog:BlogPost]').filter(this.filterstring).sort('_index', this.sorting).get() : false}

    @context.countposts = ${this.startingPoint ? Array.length(this.blogposts) : 0}
    @context.page = ${request.arguments.page}

    pagination = NeosRulez.Blog:FusionPageBrowser {
        posts = ${q(node).property('posts')}
        countposts = ${countposts}
        page = ${page}
    }

    blogpostsoutput = ${this.startingPoint ? q(this.startingPoint).children('[instanceof NeosRulez.Blog:BlogPost]').slice(this.pagination.queryOffset,this.pagination.queryItems).filter(this.filterstring).sort('_index', this.sorting).get() : false}

    #@cache {
    #    mode = 'uncached'
    #    context {
    #        1 = 'node'
    #        2 = 'documentNode'
    #    }
    #}
}

prototype(NeosRulez.Blog:FusionPageBrowser) {
    @class = 'NeosRulez\\Blog\\Fusion\\PageBrowserImplementation'
}
