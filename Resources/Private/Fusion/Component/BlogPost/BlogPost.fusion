prototype(NeosRulez.Blog:Component.BlogPost) < prototype(Neos.Fusion:Component) {

    blogdate = ${documentNode.properties.blogdate ? Date.format(documentNode.properties.blogdate, 'd.m.Y') : false}
    blogauthor = ${documentNode.properties.blogauthor}
    show_date_author_separator = ${this.blogdate ? (this.blogauthor ? true : false) : false}
    blogimages = ${documentNode.properties.blogimages ? documentNode.properties.blogimages : []}
    show_gallery = ${Array.length(this.blogimages) > 1 ? true : false}
    blogcategories = ${documentNode.properties.blogcategories}
    title = ${documentNode.properties.title}
    show_comments = ${documentNode.properties.enablecomments}

    nodeuri = Neos.Neos:NodeUri {
        node = ${documentNode}
        absolute = true
    }

    twittername = ${Configuration.setting('NeosRulez.Blog.twitterAccountName')}

    main = Neos.Neos:ContentCollection {
        nodePath = 'main'
    }

    dontCropFirstImage = ${q(node).property('dontCropFirstImage')}

    enablePrevNext = ${Configuration.setting('NeosRulez.Blog.navigation.enablePrevNext')}

    renderer = afx`
        <div class="neosrulez-blog-blogpost">
            <div class="container">
                <div @if.render={props.blogimages} class="mb-4 blogpost-image">
                    <NeosRulez.Bootstrap:Component.ImageTag image={props.blogimages[0]} width="1320" height={props.dontCropFirstImage ? false : 650} allowCropping={!props.dontCropFirstImage} allowUpScaling="true" async="true" />
                </div>
                <h1><Neos.Neos:Editable property="title" /></h1>
                <div class="d-block blogpost-teaser">
                    <Neos.Neos:Editable property="blogsubtitle" />
                </div>
                <div class="row align-items-center">
                    <div class="col-md-6">
                        <span @if.render={props.blogauthor} class="blogpost-author">{props.blogauthor}</span>
                        <span @if.render={props.show_date_author_separator} >, </span>
                        <span @if.render={props.blogdate} class="blogpost-date">{props.blogdate}</span>
                    </div>
                    <div class="col-md-6">
                        <NeosRulez.Blog:Component.BlogShare header={props.title} url={props.nodeuri} twitterName={props.twittername} />
                    </div>
                </div>

                <div @if.render={props.blogcategories} class="d-block my-3 blogpost-categories">
                    <NeosRulez.Blog:Component.BlogCategories categories={props.blogcategories} />
                </div>
                <div class="d-block mt-4 blogpost-content">
                    <Neos.Neos:Editable property="blogcontent" />
                </div>
                <NeosRulez.Blog:Component.BlogPostBackLink />
                <div @if.render={props.show_gallery} class="mt-5 blogpost-gallery">
                    <NeosRulez.Bootstrap:Content.Gallery assets={props.blogimages} lightbox={true} columns="col-6 col-md-3" />
                </div>
                <NeosRulez.Blog:Component.Navigation @if.render={props.enablePrevNext} />
            </div>
        </div>
        {props.main}
        <NeosRulez.Blog:Component.BlogComments @if.render={props.show_comments} />
    `
    renderer.@process.contentElementWrapping = Neos.Neos:ContentElementWrapping {
        node = ${documentNode}
    }
}
