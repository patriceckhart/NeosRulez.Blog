prototype(NeosRulez.Blog:Component.BlogComments) < prototype(Neos.Fusion:Component) {

    comments = ${q(documentNode).children().children('[instanceof NeosRulez.Blog:Content.BlogPostComment]').sort('_index', 'DESC').get()}
    @context.comments = ${this.comments}
    comments_count = ${Array.length(this.comments)}
    node_identifier = ${node.identifier}

    renderer = afx`
        <div id="comments" class="container">
            <div class="mt-5 neosrulez-blog-blogpost-add-comment-header">
                <h5 @if.render={props.comments_count != 0 ? true : false} >{I18n.translate('NeosRulez.Blog:NodeTypes.Content.BlogPostComment:header')}</h5>
                <h5 @if.render={props.comments_count != 0 ? false : true}>{I18n.translate('NeosRulez.Blog:NodeTypes.Content.BlogPostComment:header0')}</h5>
                <p class="mb-4 neosrulez-blog-blogpost-add-comment-header-text">{I18n.translate('NeosRulez.Blog:NodeTypes.Content.BlogPostComment:headertext')}</p>
            </div>
            <div class="neosrulez-blog-blogpost-add-comment">
                <Neos.Fusion.Form:Form form.target.action="create" form.target.controller="Comment" form.target.package="NeosRulez.Blog" form.data.args={args} >
                    <Neos.Fusion.Form:Hidden field.name="args[parentnode]" attributes.value={props.node_identifier} />
                    <div class="mb-3">
                        <label for="name">{I18n.translate('NeosRulez.Blog:NodeTypes.Content.BlogPostComment:name')}</label>
                        <Neos.Fusion.Form:Input attributes.id="name" attributes.class="form-control" field.name="args[name]" attributes.placeholder={I18n.translate('NeosRulez.Blog:NodeTypes.Content.BlogPostComment:name')} attributes.required="required" />
                    </div>
                    <div class="mb-3">
                        <label for="email">{I18n.translate('NeosRulez.Blog:NodeTypes.Content.BlogPostComment:email')}</label>
                        <Neos.Fusion.Form:Input attributes.id="email" attributes.class="form-control" field.name="args[email]" attributes.placeholder={I18n.translate('NeosRulez.Blog:NodeTypes.Content.BlogPostComment:email')} attributes.required="required" />
                    </div>
                    <div class="mb-3">
                        <label for="message">{I18n.translate('NeosRulez.Blog:NodeTypes.Content.BlogPostComment:message')}</label>
                        <Neos.Fusion.Form:Textarea attributes.id="message" attributes.class="form-control" field.name="args[message]" attributes.placeholder={I18n.translate('NeosRulez.Blog:NodeTypes.Content.BlogPostComment:message')} attributes.required="required" />
                    </div>
                    <div class="mb-3">
                        <Neos.Fusion.Form:Button attributes.class="btn btn-primary" >{I18n.translate('NeosRulez.Blog:NodeTypes.Content.BlogPostComment:submit')}</Neos.Fusion.Form:Button>
                    </div>
                </Neos.Fusion.Form:Form>
            </div>
            <div @if.render={props.comments} class="mt-5 neosrulez-blog-blogpost-comments">
                <Neos.Fusion:Loop items={comments} itemName="comment">
                    <NeosRulez.Blog:Component.BlogComments.Comment />
                </Neos.Fusion:Loop>
            </div>
        </div>
    `
    @cache {
        mode = 'cached'
        entryIdentifier {
            node = ${node}
        }
        context {
            1 = 'site'
            2 = 'node'
            3 = 'documentNode'
        }
        entryTags {
            1 = ${Neos.Caching.nodeTag(node)}
            2 = ${Neos.Caching.nodeTypeTag('NeosRulez.Blog:Content.BlogPostComment', node)}
        }
    }
}

prototype(NeosRulez.Blog:Component.BlogComments.Comment) < prototype(Neos.Fusion:Component) {

    renderer = afx`
        <div class="row mt-4">
            <div class="col-md-1 col-2">
                <NeosRulez.Blog:Helper.Gravatar email={q(comment).property('blogPostCommentAuthorEmail')} alt={q(comment).property('blogPostCommentAuthorName')} />
            </div>
            <div class="col-md-11 col-10">
                <strong class="d-block">{q(comment).property('blogPostCommentAuthorName')}</strong>
                <span class="d-block text-muted">
                    <NeosRulez.Blog:Helper.DateHelper date={q(comment).property('blogPostCommentdate')} format="d.m.Y, H:i" />
                </span>
                <span class="d-block mt-2">
                    <NeosRulez.Blog:Helper.StripTags text={q(comment).property('blogPostCommentMessage')} />
                </span>
            </div>
        </div>
    `
}
