prototype(NeosRulez.Blog:Component.BlogPagination) < prototype(Neos.Fusion:Component) {

    @context.posts = ${this.posts}
    @context.countposts = ${this.countposts}
    @context.page = ${this.page}
    node = ${this.node}
    page = ${this.page}
    showFirstLast = false

    pagination = NeosRulez.Blog:Helper.Pagination {
        posts = ${posts}
        countposts = ${countposts}
        page = ${page}
    }

    nodeuri = Neos.Neos:NodeUri {
        node = ${documentNode}
        absolute = true
    }

    renderer = afx`
        <nav class="blog-pagination" aria-label="blog-pagination">
            <ul class="pagination">
                <li @if.render={props.pagination.prev} class="page-item prev-item">
                    <a href={props.nodeuri + '/page/' + props.pagination.prev} class="page-link" >
                        <i class="fas fa-angle-left"></i>
                    </a>
                </li>
                <li @if.render={props.pagination.prev && props.showFirstLast} class="page-item first-page">
                    <a href={props.nodeuri + '/page/1'} class="page-link" >
                        <i class="fas fa-angle-double-left"></i>
                    </a>
                </li>
                <Neos.Fusion:Loop items={props.pagination.pagination.pages} itemName="item">
                    <li class={'page-item' + (String.toInteger(item.text) == String.toInteger(props.page) ? ' active' : '')} >
                        <a href={props.nodeuri + '/page/' + item.text} class="page-link" >
                            {item.text}
                        </a>
                    </li>
                </Neos.Fusion:Loop>
                <li @if.render={props.pagination.next && props.showFirstLast} class="page-item last-page">
                    <a href={props.nodeuri + '/page/' + Array.length(props.pagination.pagination.pages)} class="page-link" >
                        <i class="fas fa-angle-double-right"></i>
                    </a>
                </li>
                <li @if.render={props.pagination.next} class="page-item next-item">
                    <a href={props.nodeuri + '/page/' + props.pagination.next} class="page-link" >
                        <i class="fas fa-angle-right"></i>
                    </a>
                </li>
            </ul>
        </nav>
    `
}
