prototype(NeosRulez.Blog:BlogPosts) < prototype(Neos.Neos:Content) {
    templatePath = 'resource://NeosRulez.Blog/Private/Templates/NodeTypes/BlogPosts.html'

    pagebrowser = ${q(node).property('pagebrowser')}
    showdate = ${q(node).property('showdate')}
    showsubtitle = ${q(node).property('showsubtitle')}
    showaslist = ${q(node).property('showaslist')}

    startingPoint = ${q(node).property('nodeidentifier')}
    sorting = ${q(node).property('sorting')}
    filter = Neos.Fusion:Collection {
        collection = ${q(node).property('blogcategories')}
        itemName = 'category'
        itemKey = 'key'
        itemRenderer = ${category.identifier + (iterator.isLast ? '' : ', ')}
    }
    filterstring = ${'[blogcategories *= "' + this.filter + '"]'}
    blogposts = ${q(this.startingPoint).children('[instanceof NeosRulez.Blog:BlogPost]').filter(this.filterstring).sort('_index', this.sorting).get()}
    @context.countposts = ${Array.length(this.blogposts)}

    pagebrowserImplementation = NeosRulez.Blog:FusionPageBrowser {
        collection = ${this.blogposts}
        posts = ${q(node).property('posts')}
        countposts = ${this.countposts}
    }

    @cache {
        mode = 'uncached'
        context {
            1 = 'node'
            2 = 'documentNode'
        }
    }
}

prototype(NeosRulez.Blog:FusionPageBrowser) {
    @class = 'NeosRulez\\Blog\\Fusion\\PageBrowserImplementation'
}
